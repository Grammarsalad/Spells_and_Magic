///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////
// Functions used by morpheus562                                         //
///////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Borrowed from aVENGER to assist in updating CLAB files                //
///////////////////////////////////////////////////////////////////////////
DEFINE_PATCH_FUNCTION set_clab_2da_entries INT_VAR f_MaxLevel = 50 f_Increment = 100 BEGIN
  COUNT_2DA_COLS num_col
  READ_2DA_ENTRIES_NOW r2en num_col
  c = 0
  newrow = r2en + 1
  SPRINT newrow "fl#row%newrow%"
  FOR (i=f_MinLevel;i < num_col AND i <= f_MaxLevel;i += f_Increment) BEGIN
    SPRINT cell ""
    FOR (j = 0;!"%cell%" STRING_EQUAL_CASE "****" AND j < r2en;++j) BEGIN
      READ_2DA_ENTRY_FORMER r2en j i cell
      PATCH_IF "%cell%" STRING_EQUAL_CASE "****" BEGIN
        SET_2DA_ENTRY_LATER s2el j i "%f_Entry%"
      END
    END
    PATCH_IF !"%cell%" STRING_EQUAL_CASE "****" BEGIN
      FOR (j = c;j < i - 1;++j) BEGIN //- 1 because that's where we'd want our entry
        SPRINT newrow "%newrow% ****"
        ++c
      END
      SPRINT newrow "%newrow% %f_Entry%"
      ++c
    END
  END
  SET_2DA_ENTRIES_NOW s2el num_col
  PATCH_IF c > 0 BEGIN
    FOR (i = c;i < num_col - 1;++i) BEGIN //- 1 because num_col includes the row name
      SPRINT newrow "%newrow% ****"
    END
    INSERT_2DA_ROW r2en num_col "%newrow%"
  END
END

/*
///////////////////////////////////////////////////////////////////////////
// Created by morpheus562 to add HLA rows to luxxx.2da files             //
///////////////////////////////////////////////////////////////////////////
DEFINE_ACTION_FUNCTION ADD_HLA_ROW
  INT_VAR
    class = 2       // Class ID to add HLAs to
    num_hlas = 1    // Number of New HLA rows to create
  BEGIN

  // Find class in class.ids
  COPY_EXISTING ~CLASS.IDS~ ~override~
  COUNT_2DA_ROWS 2 rows
  PATCH_IF (%class% > 0) AND (%class% < rows) BEGIN
    READ_2DA_ENTRY class 1 2 class_name
  END ELSE BEGIN
    PATCH_PRINT ~The selected class number, %class%, is out of bounds.~
  END

  // Find class in luabbr.2da
  COPY_EXISTING ~LUABBR.2DA~ ~override~
  COUNT_2DA_ROWS 2 rows
  FOR (index = 1; index < rows; ++index) BEGIN
    READ_2DA_ENTRY index 0 2 row_name
    PATCH_IF ~%row_name%~ STRING_EQUAL_CASE ~%class_name%~ BEGIN
      READ_2DA_ENTRY index 1 2 classlu
    END
  END

  // Add the new HLA to the correct class luxxx.2da file
  ACTION_IF FILE_EXISTS_IN_GAME ~lu%classlu%.2da~ BEGIN
    COPY_EXISTING ~lu%classlu%.2da~ ~override~
      FOR (i = 1; i <= num_hlas; ++i)
        LPF ADD_NEW_HLA_ROW END
        PRETTY_PRINT_2DA
      END
    BUT_ONLY
  END

END
*/

///////////////////////////////////////////////////////////////////////////
// Created by morpheus562 to add new HLA abilities to luxxx.2da files    //
///////////////////////////////////////////////////////////////////////////
DEFINE_ACTION_FUNCTION ADD_HLA
  INT_VAR
    class = 2       // Class ID to add HLAs to
    min_lev = 1     // Min Lev to receive HLA
    max_lev = 99    // Max Level to receive HLA
    num_allowed = 1 // Number of times the ability can be taken
  STR_VAR
    ability = ""       // New ability being added 
    prerequisite = "*" // Ability is required first
    excluded_by = "*"  // Excluded by field
    alignment = "*"    // Alignment requirements
  BEGIN

  // Find class in class.ids
  COPY_EXISTING ~CLASS.IDS~ ~override~
  COUNT_2DA_ROWS 2 rows
  PATCH_IF (%class% > 0) AND (%class% < rows) BEGIN
    PATCH_IF GAME_IS ~iwdee~ BEGIN
      READ_2DA_ENTRY class 1 1 class_name
    END ELSE BEGIN
      READ_2DA_ENTRY class 1 2 class_name
    END
    PATCH_PRINT ~Found class: %class% %class_name%.~
  END ELSE BEGIN
    PATCH_PRINT ~The selected class number, %class%, is out of bounds.~
  END

  // Find class in luabbr.2da
  COPY_EXISTING ~LUABBR.2DA~ ~override~
  COUNT_2DA_ROWS 2 rows
  FOR (index = 1; index < rows; ++index) BEGIN
    READ_2DA_ENTRY index 0 2 row_name
    PATCH_IF ~%row_name%~ STRING_EQUAL_CASE ~%class_name%~ BEGIN
      READ_2DA_ENTRY index 1 2 classlu
    END
  END

  // Add the new HLA to the correct class luxxx.2da file
  ACTION_IF FILE_EXISTS_IN_GAME ~lu%classlu%.2da~ BEGIN
    COPY_EXISTING ~lu%classlu%.2da~ ~override~
      LPF ADD_NEW_HLA_ROW END
      LPF ADD_NEW_HLA INT_VAR min_lev = %min_lev% max_lev = %max_lev% num_allowed = %num_allowed% STR_VAR ability = ~%ability%~ prerequisite = ~%prerequisite%~ END
      PRETTY_PRINT_2DA
    BUT_ONLY
  END

  // ADD HLAS TO KITS -----------------------------------------------------//
  // Find kits in kitlist
  COPY_EXISTING ~KITLIST.2DA~ ~override~
  READ_2DA_ENTRIES_NOW ~kits~ 10
  FOR (row = 1; row < kits; row += 1) BEGIN
  READ_2DA_ENTRY_FORMER ~kits~ row 5 kit_clab
  READ_2DA_ENTRY_FORMER ~kits~ row 1 kit_label
  READ_2DA_ENTRY_FORMER ~kits~ row 8 kit_class
  
  PATCH_PRINT ~Checking %kit_class%~
  PATCH_IF NOT ~%kit_class%~ STRING_EQUAL_CASE ~*~ BEGIN
    PATCH_IF (%kit_class% = %class%) BEGIN
      // Add HLAs to all Class Kits
      INNER_ACTION BEGIN
      COPY_EXISTING ~LUABBR.2DA~ ~override~
      COUNT_2DA_ROWS 2 rows
      FOR (index = 1; index < rows; ++index) BEGIN
        READ_2DA_ENTRY index 0 2 row_name
        PATCH_IF ~%row_name%~ STRING_EQUAL_CASE ~%kit_label%~ BEGIN
          READ_2DA_ENTRY index 1 2 kitlu
          PATCH_PRINT ~Checking if lu%kitlu%.2da exists.~
          PATCH_IF FILE_EXISTS_IN_GAME ~lu%kitlu%.2da~ BEGIN
          PATCH_PRINT ~lu%kitlu%.2da exists and is being patched.~
            INNER_ACTION BEGIN
            COPY_EXISTING ~lu%kitlu%.2da~ ~override~
              LPF ADD_NEW_HLA_ROW END
              LPF ADD_NEW_HLA INT_VAR min_lev = %min_lev% max_lev = %max_lev% num_allowed = %num_allowed% STR_VAR ability = ~%ability%~ prerequisite = ~%prerequisite%~ END
              PRETTY_PRINT_2DA
            BUT_ONLY
            END
          END
        END
      END
      END
    END
  END
  END
END

///////////////////////////////////////////////////////////////////////////
// Created by morpheus562 to add new HLA rows to luxxx.2da files         //
///////////////////////////////////////////////////////////////////////////
DEFINE_PATCH_FUNCTION ADD_NEW_HLA_ROW BEGIN
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  SET row_num = rows + 1
  PATCH_IF (cols = 10) BEGIN  
    INSERT_2DA_ROW ("%rows%") %cols% ~%row_num% * * * * * * * * *~
  END ELSE BEGIN
    INSERT_2DA_ROW ("%rows%") %cols% ~%row_num% * * * * * * * *~
  END
END

///////////////////////////////////////////////////////////////////////////
// Created by morpheus562 to add new HLA abilities to luxxx.2da files    //
///////////////////////////////////////////////////////////////////////////
DEFINE_PATCH_FUNCTION ADD_NEW_HLA
  INT_VAR
    min_lev = 1     // Min Lev to receive HLA
    max_lev = 99    // Max Level to receive HLA
    num_allowed = 1 // Number of times the ability can be taken
  STR_VAR
    ability = ""       // New ability being added
    prerequisite = "*" // Ability is required first
    excluded_by = "*"  // Excluded by field
    alignment = "*"    // Alignment requirements
  BEGIN  
  COUNT_2DA_COLS kcols
  COUNT_2DA_ROWS 4 krows
  FOR (j = 1; j < krows; ++j) BEGIN
    READ_2DA_ENTRY j 1 4 classhla
    PATCH_IF ~%classhla%~ STRING_EQUAL ~%ability%~ BEGIN
      j = krows
    END ELSE PATCH_IF (~%classhla%~ STRING_EQUAL ~*~) AND (kcols = 10) BEGIN
      SET_2DA_ENTRY (j+2) 1 1 ~%ability%~      // HLA Name
      SET_2DA_ENTRY (j+2) 4 1 ~%min_lev%~      // MIN_LEV
      SET_2DA_ENTRY (j+2) 5 1 ~%max_lev%~      // MAX_LEV
      SET_2DA_ENTRY (j+2) 6 1 ~%num_allowed%~  // NUM_ALLOWED
      SET_2DA_ENTRY (j+2) 7 1 ~%prerequisite%~ // Prerequisite HLA
      SET_2DA_ENTRY (j+2) 8 1 ~%excluded_by%~  // Excluded by HLA
      SET_2DA_ENTRY (j+2) 9 1 ~%alignment%~    // Required Alignment
      j = krows
    END ELSE PATCH_IF ~%classhla%~ STRING_EQUAL ~*~ BEGIN
      SET_2DA_ENTRY (j+2) 1 1 ~%ability%~      // HLA Name
      SET_2DA_ENTRY (j+2) 4 1 ~%min_lev%~      // MIN_LEV
      SET_2DA_ENTRY (j+2) 5 1 ~%max_lev%~      // MAX_LEV
      SET_2DA_ENTRY (j+2) 6 1 ~%num_allowed%~  // NUM_ALLOWED
      SET_2DA_ENTRY (j+2) 7 1 ~%prerequisite%~ // Prerequisite HLA
      SET_2DA_ENTRY (j+2) 8 1 ~%excluded_by%~  // Excluded by HLA
      j = krows
    END
  END
END
